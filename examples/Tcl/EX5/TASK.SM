// -*- tab-width: 4; -*-
//
// The contents of this file are subject to the Mozilla Public
// License Version 1.1 (the "License"); you may not use this file
// except in compliance with the License. You may obtain a copy of
// the License at http://www.mozilla.org/MPL/
// 
// Software distributed under the License is distributed on an "AS
// IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
// implied. See the License for the specific language governing
// rights and limitations under the License.
// 
// The Original Code is State Machine Compiler (SMC).
// 
// The Initial Developer of the Original Code is Charles W. Rapp.
// Portions created by Charles W. Rapp are
// Copyright (C) 2000 Charles W. Rapp.
// All Rights Reserved.
// 
// Contributor(s): 
//
// Task --
//
//  A task's sole responsiblity is to execute. This state machine
//  is used to track whether the task is running, suspended,
//  blocked or stopped.
//
// RCS ID
// $Id$
//
// CHANGE LOG
// $Log$
// Revision 1.2  2002/02/19 19:52:48  cwrapp
// Changes in release 1.3.0:
// Add the following features:
// + 479555: Added subroutine/method calls as argument types.
// + 508878: Added %import keyword.
//
// Revision 1.1.1.1  2001/01/03 03:14:00  cwrapp
//
// ----------------------------------------------------------------------
// SMC - The State Map Compiler
// Version: 1.0, Beta 3
//
// SMC compiles state map descriptions into a target object oriented
// language. Currently supported languages are: C++, Java and [incr Tcl].
// SMC finite state machines have such features as:
// + Entry/Exit actions for states.
// + Transition guards
// + Transition arguments
// + Push and Pop transitions.
// + Default transitions. 
// ----------------------------------------------------------------------
//
// Revision 1.1.1.1  2000/08/02 12:51:05  charlesr
// Initial source import, SMC v. 1.0, Beta 1.
//

%start ExecuteMap::Suspended
%class Task

%map ExecuteMap
%%
// A task begins life in suspended animation.
Suspended
{
    Start       Running         {sendMessage(3, "Task is running.");
                                 stateUpdate("Running");
                                }
}

// This task is now running.
Running
Entry {setStartTime();
       setRunTimer();
      }
Exit {setStopTime();
      stopTimer("Done");
     }
{
    Suspend     Suspended       {sendMessage(3, "Task is being suspended.");
                                 stateUpdate("Suspended");
                                 setSuspendTime();
                                }

    Done        Stopped         {sendMessage(3, "Task completed.");
                                 stateUpdate("Done");
                                 updateTaskMan("taskDone");
                                }
}

Blocked
{
    Unblock     Suspended       {sendMessage(3, "Task unblocked.");
                                 stateUpdate("Suspended");
                                }

    Block       nil             {}
}

Stopping
{
    Stopped     Stopped         {sendMessage(3, "Task stopped.");
                                 stateUpdate("Stopped");
                                 updateTaskMan("taskStopped");
                                }

    Delete      nil             {}
}

Stopped
{
    // Now that the task is stopped, ignore
    // requests to block or stop.
    Block       nil             {}
    Stop        nil             {}
    Delete      nil             {}
}

Deleted
{
    Block       nil             {}
    Stop        nil             {}
    Delete      nil             {}
}

Default
{
    // Do blocks, stops and deletes immediately.
    Block       Blocked         {sendMessage(3, "Task blocked.");
                                 stateUpdate("Blocked");
                                }
    Stop        Stopping        {sendMessage(3, "Task is being stopped.");
                                 setTimer("Stopped", idle);
                                }
    Delete      Deleted         {sendMessage(3, "Task deleted.");
                                 stateUpdate("Deleted");
                                 updateTaskMan("taskDeleted");
                                }

    // Ignore all other transitions unless in the
    // proper state.
    Start       nil             {}
    Started     nil             {}
    Suspend     nil             {}
    Suspended   nil             {}
    Unblock     nil             {}
    Done        nil             {}
}

%%
